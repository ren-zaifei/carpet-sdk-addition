plugins {
    id "me.modmuss50.mod-publish-plugin" version "0.8.3"
}

apply plugin: 'fabric-loom'
apply plugin: 'com.replaymod.preprocess'

int mcVersion = 1

ext {
    getVersionSuffix = {
        String buildNumber
        String buildType = 'build'
        if (System.getenv("CI_BUILD") == 'false') buildNumber = null
        else {
            if (System.getenv("PR_BUILD") != 'false') buildType = 'pr'
            buildNumber = System.getenv("GITHUB_RUN_NUMBER")
        }
        String versionSuffix = (buildNumber != null ? "+${buildType}.${buildNumber}" : "")
        return versionSuffix
    }
}

preprocess {
    mcVersion = vars.get()["MC"] as int
    tabIndentation = true
}

configurations {
    modRuntimeOnly.exclude group: 'net.fabricmc', module: 'fabric-loader'
}

repositories {
    mavenLocal()
    maven {
        name 'mavenCentral'
        url 'https://repo1.maven.org/maven2/'
    }
    maven {
        name 'MasaMaven'
        url 'https://masa.dy.fi/maven'
    }
    maven {
        name 'CjsahMaven'
        url 'https://server.cjsah.net:1002/maven/'
    }
    maven {
        name 'CurseMaven'
        url "https://www.cursemaven.com"
    }
}

loom {
    accessWidenerPath = file("csdk.accesswidener")
}

//remapJar {
//    remapperIsolation = true
//}

JavaVersion SOURCE_JAVA_VERSION
JavaVersion TARGET_JAVA_VERSION
if (mcVersion >= 12100) {
    SOURCE_JAVA_VERSION = JavaVersion.VERSION_21
    TARGET_JAVA_VERSION = JavaVersion.VERSION_21
}
JavaVersion MIXIN_COMPATIBILITY_LEVEL = TARGET_JAVA_VERSION

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
    }
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // fabric-carpet
    modImplementation("carpet:fabric-carpet:${project.carpet_core_version}")
}

String versionSuffix = project.getVersionSuffix()

String fullModVersion = project.mod_version + versionSuffix

version = 'v' + fullModVersion
group = project.maven_group

String fabric_id = "fabric-api"

processResources {
    from "csdk.accesswidener"

    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        def valueMap = [
                "version"          : fullModVersion,
                "id"               : project.mod_id,
                "name"             : project.archives_base_name,
                "minecraft_support": project.minecraft_support_version,
                "fabric_loader"    : project.loader_version,
                "fabric_api_id"    : fabric_id
        ]
        expand valueMap
    }

    filesMatching("quickbackupmulti.mixins.json") {
        filter {
            text -> text.replace("/*JAVA_VERSION*/", "JAVA_${MIXIN_COMPATIBILITY_LEVEL.ordinal() + 1}")
        }
    }
}

java {
    withSourcesJar()
    sourceCompatibility = SOURCE_JAVA_VERSION
    targetCompatibility = TARGET_JAVA_VERSION
}

publishMods {
    file = remapJar.archiveFile
    changelog = "Changelog"
    type = BETA
    modLoaders.add("fabric")

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "Sub5yYXA"
        minecraftVersions.add(project.game_versions)
    }
}


jar {
    archiveBaseName.set(project.archives_base_name+'-mc'+project.minecraft_version)
    from "LICENSE"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}